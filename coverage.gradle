allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    apply plugin: 'kotlin'
}

configurations { codacy }

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

task jacocoRootReport(type: JacocoReport, dependsOn: subprojects.test) {
    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = subprojects.stream().map {fileTree(it.buildDir).include("/jacoco/*.exec")}.collect()
    reports {
        xml.enabled = true
    }
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoRootReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = ["report",
            "-l",
            "Kotlin",
            "-r",
            "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
    ]
}
