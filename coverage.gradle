allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    apply plugin: 'kotlin'
}

configurations { codacy }

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

def jacocoRootReportPath = "${buildDir}/reports/jacoco/report.xml"

task codeCoverageReport(type: JacocoReport, dependsOn: subprojects.test) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    subprojects.each { sourceSets it.sourceSets.main }
    reports {
        xml.enabled = true
        xml.destination = new File(jacocoRootReportPath)
        html.enabled = false
        csv.enabled = false
    }
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: codeCoverageReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = ["report",
            "-l",
            "Kotlin",
            "-r",
            jacocoRootReportPath
    ]
}
