plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
    id 'org.jetbrains.kotlin.plugin.spring' version "${kotlinVersion}" apply false
    id 'org.jetbrains.kotlin.plugin.jpa' version "${kotlinVersion}" apply false
    id 'org.jetbrains.kotlin.plugin.allopen' version "${kotlinVersion}" apply false
    id 'org.jetbrains.kotlin.plugin.noarg' version "${kotlinVersion}" apply false
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}" apply false
}

group 'io.gitplag'
version '0.5'

apply from: 'coverage.gradle'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'io.github.microutils:kotlin-logging:1.6.22'
        implementation 'com.beust:klaxon:5.0.1'

        testImplementation 'org.mockito:mockito-inline:2.24.5'
        testImplementation('com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0') {
            exclude group: 'org.jetbrains.kotlin'
            exclude group: 'org.mockito'
        }

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
        testImplementation "io.kotlintest:kotlintest:2.0.7"
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    compileKotlin { kotlinOptions.jvmTarget = "1.8" }
    compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
